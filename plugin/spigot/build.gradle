import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
  id 'java'
  id 'com.github.johnrengelman.shadow'
  id 'com.github.node-gradle.node'
}

group 'com.jonathanpotts'
version '1.0-SNAPSHOT'

repositories {
  mavenCentral()
  maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
  maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots' }
}

dependencies {
  implementation project(':shared')

  compileOnly 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'
}

node {
  nodeProjectDir = file("$rootDir/../web")
}

tasks.jar {
  archivesBaseName = "${rootProject.name}-${project.name}"
}

tasks.shadowJar {
  archivesBaseName = "${rootProject.name}-${project.name}"
}

tasks.register('npmBuild', NpmTask) {
  dependsOn 'npmInstall'
  args = ['run', 'build']
}

tasks.register('copyWeb', Copy) {
  dependsOn 'npmBuild'
  from "$rootDir/../web/dist"
  into "$buildDir/resources/main/data/web"
}

tasks.register('webJar', Jar) {
  dependsOn 'copyWeb'
  from sourceSets.main.output
}

tasks.register('webShadowJar', ShadowJar) {
  dependsOn 'copyWeb'
  from sourceSets.main.runtimeClasspath
  archiveClassifier = 'all'
}
