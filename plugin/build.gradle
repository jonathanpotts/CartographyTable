plugins {
  id 'java'
  id 'com.github.node-gradle.node' version '3.0.1'
}

group 'com.jonathanpotts'
version '1.0-SNAPSHOT'

repositories {
  mavenCentral()
  maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
  maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots' }
  maven { url 'https://repository.mulesoft.org/nexus/content/repositories/public/' }
}

java {
  sourceCompatibility JavaVersion.VERSION_16
  targetCompatibility JavaVersion.VERSION_16
}

dependencies {
  implementation 'com.github.imcdonagh:image4j:0.7.2'

  compileOnly 'org.spigotmc:spigot-api:1.17-R0.1-SNAPSHOT'
}

node {
  nodeProjectDir = file("$rootDir/../web")
}

tasks.jar {
  archivesBaseName = "${project.name}"
}

tasks.register('npmBuild', NpmTask) {
  dependsOn 'npmInstall'
  args = ['run', 'build']
}

tasks.register('copyWeb', Copy) {
  dependsOn 'npmBuild'
  from "$rootDir/../web/dist"
  into "$buildDir/resources/main/data/web"
}

tasks.register('webJar', Jar) {
  dependsOn 'copyWeb'
  from sourceSets.main.output
}
